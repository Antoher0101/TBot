plugins {
    id "com.google.protobuf" version "0.9.4"
}

var protocVersion = protobufVersion

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.liquibase:liquibase-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'org.freemarker:freemarker:2.3.33'
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"

    runtimeOnly 'org.postgresql:postgresql'
    testRuntimeOnly 'com.h2database:h2'
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "io.grpc:grpc-protobuf:${grpcVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}
bootJar {
    enabled = true
}

bootBuildImage{
    imageName='mawus/'+rootProject.name+'-'+project.name
}